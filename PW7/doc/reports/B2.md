## B2 Mem2Reg

#### B2-1

遍历模块中的所有函数，对每个函数做以下处理：

1. 首先判断当前函数是否包含基本块，若不包含则不需要处理，否则继续进行后面的流程。

2. 使用 `insideBlockForwarding()` 进行基本块内优化，移除不必要的 `store` 和 `load` 指令。其基本思想为如果一条 `store` 指令将一个值存在了内存中，后续又有一条 `load` 指令从内存中提取出这个值并使用，那么显然这个先 `store` 再 `load` 的过程是不需要的，我们可以直接使用这个值并且删除相应的 `store` 和 `load` 指令。主要操作如下：

   1. 遍历基本块内的所有指令，对于所有可能被优化的指令（`isLocalVarOp` 为真的指令）进行处理：
      * 对于 `store` 指令，若其 `rvalue` 是 `load` 指令的右值并且该 `load` 指令在 `forward_list` 中，则直接使用相应的值替换 `rvalue`；若其 `lvalue` 在 `defined_list` 中，则表明 `lvalue` 在 `defined_list` 指令的定值被注销，将其添加到 `delete_list` 中并更新 `lvalue` 在 `defined_list` 中对应的指令为当前的 `store` 指令，否则将当前指令及其左值插入到 `defined_list` 中；若其 `lvalue` 在 `new_value` 中，则更新其右值为当前指令的 `rvalue`，否则将当前指令的左值右值插入到 `new_value` 中。
      * 对于 `load` 指令，若其 `lvalue` 不在 `new_value` 中，则不需要处理，否则在 `new_value` 中找到 `lvalue` 对应的右值，将当前指令和该右值插入到 `forward_list` 中。
   2. 遍历 `forward_list`，对于其中的每一个 `inst-value` 对，遍历 `inst` 的 `use_list`，将其中使用 `inst` 的操作数替换为 `value`，最后删除指令 `inst`。
   3. 遍历 `delete_list`，删除其中的所有指令。

3. 使用 `genPhi` 生成 $\phi$ 指令，其基本思想为如果一个基本块中的变量在其多个前驱基本块中都有定义且都可以传播到当前基本块，那么我们就需要生成一条 $\phi$ 指令来确定当前基本块入口处该变量的取值。主要操作如下：

   * 对于 `load` 指令，将其 `lvalue` 加入到 `globals`中（经过了 `insideBlockForwarding` 的处理后，剩下的 `load` 指令的左值都是跨基本块作用的）。
   * 对于 `store` 指令，将其 `lvalue` 对应的基本块加入到 `define_in_block` 中。
   * 遍历 `globals` 中的变量，获取定义该变量的基本块并初始化队列，遍历队列中的基本块，遍历基本块的支配前沿，检查支配前沿基本块是否已经存在于 `bb_phi_list` 中。如果存在且没有该变量的 Phi 函数，则创建一个新的 $\phi$ 函数并插入到基本块的指令列表开头。如果不存在，则直接创建并插入新的 Phi 函数，同时将基本块添加到队列中。

4. 使用 `valueDefineCounting`，记录需要重命名的变量的数目并为它们添加唯一的下标。

5. 使用 `valueForwarding`，实现变量的重命名并删除不必要的指令。主要操作如下：

   1. 初始化一个集合用于存储需要删除的指令，并将当前基本块标记为已访问。

   2. 遍历基本块中的指令，将所有 $\phi$ 指令添加到 `value_status` 中。

   3. 遍历基本块中的指令，对于所有左值不是全局变量或数组指针的 `store`，`load`指令（即 `isLocalVarOp `为真的指令 ）进行处理：

      * 若其为 `load` 指令，从 `value_status` 中查找其左值对应的最新值 `new_value` 并将当前 `load` 指令的所有使用替换为新的值。
      * 若其为 `store` 指令，如果 `value_status` 中已经存在其左值，则将右值添加到值列表中。否则，创建新的值列表，并将右值插入到 `value_status` 中。

      最后将当前指令添加到 delete_list 中，以便在后续步骤中删除这些指令。

   4. 遍历当前基本块的所有后继基本块，处理其中的 $\phi$ 指令，更新其操作数：

      检查 value_status 中是否存在该左值：如果存在，则进一步检查值列表的大小。如果值列表的大小大于 0，则获取最新的值 `new_value`，并将 `new_value` 和当前基本块添加到该 $\phi$ 指令的操作数对中。（如果值列表为空或 value_status 中不存在该左值，则提示未定义的值被使用。这一部分代码注释掉了。）

   5. 递归调用 `valueForwarding` 方法，处理后继基本块。

   6. 更新当前基本块中定义的变量的值状态：

      对于每一个变量，检查 `value_status` 中是否存在该变量。如果不存在，则跳过该变量，继续处理下一个变量。如果存在，检查 `value_status` 中该变量对应的值列表是否为空。如果为空，则跳过该变量，继续处理下一个变量。否则从 `value_status` 中该变量对应的值列表中移除最后一个值。

   7. 遍历 `delete_list` 中的指令，并将其从基本块中删除。

6. 使用 `removeAlloc` 移除为整数类型，浮点数类型和指针类型分配内存的 `alloc` 指令。

#### B2-2

例子：

```c
int g;

int main() {
    int a = 5;
    int b = 10;
    int c = a + b;
    int arr[2] = {1, 2};
    g = 1;

    while (c > 0) {
        if (a % 2 == 1) {
            b = a - 1;
            a = b - 2;
        } else {
            b = a - 2;
            a = b - 1;
        }
        c = a + b;
    }

    return c;
}
```

未开启优化时的 IR：

```asm
@g = global i32 zeroinitializer
declare i32 @get_int()

declare float @get_float()

declare i32 @get_char()

declare i32 @get_int_array(i32*)

declare i32 @get_float_array(float*)

declare void @put_int(i32)

declare void @put_float(float)

declare void @put_char(i32)

declare void @put_int_array(i32, i32*)

declare void @put_float_array(i32, float*)

define i32 @main() {
label_entry:
  %op0 = alloca i32
  %op1 = alloca i32
  store i32 5, i32* %op1
  %op2 = alloca i32
  store i32 10, i32* %op2
  %op3 = alloca i32
  %op4 = load i32, i32* %op1
  %op5 = load i32, i32* %op2
  %op6 = add i32 %op4, %op5
  store i32 %op6, i32* %op3
  %op7 = alloca [2 x i32]
  %op8 = getelementptr [2 x i32], [2 x i32]* %op7, i32 0, i32 0
  store i32 1, i32* %op8
  %op9 = getelementptr [2 x i32], [2 x i32]* %op7, i32 0, i32 1
  store i32 2, i32* %op9
  store i32 1, i32* @g
  br label %label11
label_ret:                                                ; preds = %label22
  %op10 = load i32, i32* %op0
  ret i32 %op10
label11:                                                ; preds = %label_entry, %label34
  %op12 = load i32, i32* %op3
  %op13 = icmp sgt i32 %op12, 0
  %op14 = zext i1 %op13 to i32
  %op15 = icmp ne i32 %op14, 0
  br i1 %op15, label %label16, label %label22
label16:                                                ; preds = %label11
  %op17 = load i32, i32* %op1
  %op18 = srem i32 %op17, 2
  %op19 = icmp eq i32 %op18, 1
  %op20 = zext i1 %op19 to i32
  %op21 = icmp ne i32 %op20, 0
  br i1 %op21, label %label24, label %label29
label22:                                                ; preds = %label11
  %op23 = load i32, i32* %op3
  store i32 %op23, i32* %op0
  br label %label_ret
label24:                                                ; preds = %label16
  %op25 = load i32, i32* %op1
  %op26 = sub i32 %op25, 1
  store i32 %op26, i32* %op2
  %op27 = load i32, i32* %op2
  %op28 = sub i32 %op27, 2
  store i32 %op28, i32* %op1
  br label %label34
label29:                                                ; preds = %label16
  %op30 = load i32, i32* %op1
  %op31 = sub i32 %op30, 2
  store i32 %op31, i32* %op2
  %op32 = load i32, i32* %op2
  %op33 = sub i32 %op32, 1
  store i32 %op33, i32* %op1
  br label %label34
label34:                                                ; preds = %label24, %label29
  %op35 = load i32, i32* %op1
  %op36 = load i32, i32* %op2
  %op37 = add i32 %op35, %op36
  store i32 %op37, i32* %op3
  br label %label11
}
```

开启优化后的IR：

```asm
@g = global i32 zeroinitializer
declare i32 @get_int()

declare float @get_float()

declare i32 @get_char()

declare i32 @get_int_array(i32*)

declare i32 @get_float_array(float*)

declare void @put_int(i32)

declare void @put_float(float)

declare void @put_char(i32)

declare void @put_int_array(i32, i32*)

declare void @put_float_array(i32, float*)

define i32 @main() {
label_entry:
  %op6 = add i32 5, 10
  %op7 = alloca [2 x i32]
  %op8 = getelementptr [2 x i32], [2 x i32]* %op7, i32 0, i32 0
  store i32 1, i32* %op8
  %op9 = getelementptr [2 x i32], [2 x i32]* %op7, i32 0, i32 1
  store i32 2, i32* %op9
  store i32 1, i32* @g
  br label %label11
label_ret:                                                ; preds = %label22
  ret i32 %op38
label11:                                                ; preds = %label_entry, %label34
  %op38 = phi i32 [ %op6, %label_entry ], [ %op37, %label34 ]
  %op39 = phi i32 [ 10, %label_entry ], [ %op41, %label34 ]
  %op40 = phi i32 [ 5, %label_entry ], [ %op42, %label34 ]
  %op13 = icmp sgt i32 %op38, 0
  %op14 = zext i1 %op13 to i32
  %op15 = icmp ne i32 %op14, 0
  br i1 %op15, label %label16, label %label22
label16:                                                ; preds = %label11
  %op18 = srem i32 %op40, 2
  %op19 = icmp eq i32 %op18, 1
  %op20 = zext i1 %op19 to i32
  %op21 = icmp ne i32 %op20, 0
  br i1 %op21, label %label24, label %label29
label22:                                                ; preds = %label11
  br label %label_ret
label24:                                                ; preds = %label16
  %op26 = sub i32 %op40, 1
  %op28 = sub i32 %op26, 2
  br label %label34
label29:                                                ; preds = %label16
  %op31 = sub i32 %op40, 2
  %op33 = sub i32 %op31, 1
  br label %label34
label34:                                                ; preds = %label24, %label29
  %op41 = phi i32 [ %op31, %label29 ], [ %op26, %label24 ]
  %op42 = phi i32 [ %op33, %label29 ], [ %op28, %label24 ]
  %op37 = add i32 %op42, %op41
  br label %label11
}
```

可以看到优化后的代码将变量 a，b，c 的值直接保存在了寄存器里，省去了分配内存，存储值到内存以及从内存中提取值的过程，也就是删除了未优化代码中的 `alloc`，`load`，`store`指令。此外还在循环条件所在的基本块以及条件分支后面计算变量 c 的基本块开头添加了 `phi` 指令。

各个问题说明如下：

1. `Mem2Reg`可能会删除的指令类型是哪些？对哪些分配(alloca)指令会有影响？

   可能删除的指令类型是 `alloc`，`load `和 `store `指令。具体来说，是左值不是全局变量或数组指针的 `load`，`store` 指令和为整数类型，浮点数类型和指针类型分配内存的 `alloc` 指令。

   对为整数类型，浮点数类型和指针类型分配内存的 `alloc` 指令会有影响。

   结合例子也可以发现除去全局变量 `g` 和数组 `arr` 外，其余变量的 `load` 和 `store` 指令均被删除，也只有数组 `arr` 的 `alloca` 指令被保留。

2. 在基本块内前进`insideBlockForwarding`时，对`store`指令处理时为什么`rvalue`在`forward_list`中存在时，就需要将`rvalue`替换成`forward_list`映射中的`->second`值？

   因为 `store` 指令的 `rvalue` 在 `forward_list` 中出现时，意味着它是某条 `load` 指令的左值，并且在此之前已经处理过这条 `load` 指令，更新了某个变量的值，并将最新值添加到了 `forward_list` 中，所以此时我们可以直接使用 `forward_list` 中的值而不使用 `load` 指令的左值，这样我们就可以在后面删除这条 `load` 指令。

3. 在基本块内前进时，`defined_list`代表什么含义？

   `defined_list` 代表一个映射，用于记录在当前基本块中对一个左值进行定值的最新一条 `store` 指令。

4. 生成phi指令`genPhi`的第一步两层for循环在收集什么信息，这些信息在后面的循环中如何被利用生成Phi指令？

   在收集 `globals` 和 `defined_in_block` ，它们分别存储跨基本块作用的 `load` 指令的左值和定义变量的基本块信息。

   在后面的循环中，`globals` 被用作需要生成 `Phi` 指令的变量列表，`defined_in_block`在插入 `Phi` 函数时充当了一个初始化的 `WorkList`，在代码中被用于初始化 `queue`。

5. `valueDefineCounting`为`defined_var`记录了什么信息

   记录了每个基本块中定值的左值列表，包括 `Phi` 指令和 `store` 指令的左值。

6. `valueForwarding`在遍历基本块时采用的什么方式

   采用的递归的方式，递归地处理每个基本块及其后继基本块。`valueForwarding`会先标记当前基本块为已访问，然后处理当前基本块中的 `phi` 指令和 `load`，`store` 指令，然后处理后继基本块中的 `phi` 指令，再递归处理后继基本块，最后更新 `value_status` 并删除不需要的指令。

7. `valueForwarding`中为什么`value_status`需要对phi指令做信息收集

   因为 `phi` 指令也会对左值做定值，我们也需要对 `phi` 指令定值的变量做名字重写。但是其涉及到的变量可能来自不同的基本块，如果不对 `phi` 指令做信息收集，后续就可能无法优化处理 `phi` 指令后整合产生的新名字。

8. `valueForwarding`中第二个循环对`load`指令的替换是什么含义

   意味着替换的 `load` 指令实际上是不必要存在的，因为在 `value_status` 中存在其左值的最新右值信息，所以我们可以直接使用该值而不必再使用 `load` 指令。

9. `valueForwarding`中出现的`defined_var`和`value_status`插入条目之间有什么联系

   `defined_var` 存储了基本块中定值的左值，`value_status` 中存储了这些左值对应的最新右值的信息。在处理完某一个基本块中的指令时，`define_var `中的条目恰好对应 `value_status` 中的栈顶条目。

#### B2-3

例子要求包含至少两层由条件分支、循环组成的嵌套结构，并且对同一个变量有在不同分支或者迭代中的定值和引用。根据 `Mem2Reg` 的流程，这时就需要生成 `phi` 指令。又因为变量的定值和引用会产生不必要的`alloca`， `store` 和 `load` 指令，所以 `Mem2Reg` 也会删除这些不必要的指令。综上，上述例子的要求可以体现 `Mem2Reg` 的效果。
